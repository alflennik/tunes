* async (playlists:, otherVideos:) {
  [Element:] = import("core:reconciler")
  [navigator:, document:] = import("core:web")

  &videoPlayerPlay.type(() {})<?>
  &audioDescriptionUi.type(Element)<?>
  &audioDescriptionIsPrerecorded = false

  [
    activeVideo:, 
    activePlaylist: _, 
    playContent:, 
    focusPreselectedVideo:
  ] = import("./activeVideo.iui")(
    playlists:,
    otherVideos:,
    videoPlayerPlay<?>: lock &videoPlayerPlay<?>
  )

  [isIOS, isAndroid] = [
    regex("iPhone|iPod|iPad").matches(navigator.platform),
    regex("android").modes(.caseInsensitive).matches(navigator.userAgent),
  ]

  voiceSynthesized = import("./voiceSynthesized.iui")(isIOS:, isAndroid:)
  voicePrerecorded = import("./voicePrerecorded.iui")()

  [getFirstInteractionInterceptor:, applyFirstInteractionHandlers:] = import("./firstInteraction.iui")()

  &areVoicesReady = false

  voice<?> = watch [isIOS, &audioDescriptionIsPrerecorded] {
    if !&areVoicesReady : * null ;
    if isIOS : * voiceSynthesized ; // Not usable on iOS since there is no audio ducking control
    if &audioDescriptionIsPrerecorded : * voicePrerecorded ; else : * voiceSynthesized ;
  }

  [ui: videoPlayerUi, onUiAttached: getVideoPlayer] = import("./videoPlayer.iui")(
    youtubeId: activeVideo.youtubeId,
    timeInterval: 400
  )

  rootUi = import("./rootUi.iui")(
    playlists:,
    videoPlayerUi:,
    audioDescriptionUi<?>: lock &audioDescriptionUi<?>,
    activeVideo:,
    playContent:
    getFirstInteractionInterceptor:
  )

  document.body.appendChild(rootUi.element)

  videoPlayer = await getVideoPlayer()
  &videoPlayerPlay = videoPlayer.play

  audioDescription = await import("./audioDescription.iui")(activeVideo:, videoPlayer:, voice<?>:)
  &audioDescriptionIsPrerecorded = audioDescription.isPrerecorded
  &audioDescriptionUi = audioDescription.ui

  focusPreselectedVideo()

  applyFirstInteractionHandlers(onFirstInteraction: async (isKeyDown:, isVideoPlayerInteraction:) {
    await concurrent(
      unwait voiceSynthesized.getPermissions(),
      unwait voicePrerecorded.getPermissions(),
    )
    &areVoicesReady = true
    if !isKeyDown && isVideoPlayerInteraction : videoPlayer.play() ;
  })
}

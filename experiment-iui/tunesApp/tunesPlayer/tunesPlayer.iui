* async (playlists:, otherVideos:) {
  [Element:] = import("core:reconciler")
  [navigator:, document:] = import("core:web")

  &videoPlayerPlay[%](() {})<?>
  &audioDescriptionUi[%](Element)<?>
  &audioDescriptionIsPrerecorded = false

  [
    activeVideo:, 
    // activePlaylist:, 
    playContent:, 
    focusPreselectedVideo:
  ] = import("./activeVideo.iui")(
    playlists:,
    otherVideos:,
    videoPlayerPlay<?>: &videoPlayerPlay[&]<?>
  )

  [isIOS, isAndroid] = [
    regex("iPhone|iPod|iPad").matches(navigator.platform),
    regex("android").modes("i").matches(navigator.userAgent),
  ]

  voiceSynthesized = import("./voiceSynthesized.iui")(isIOS:, isAndroid:)
  voicePrerecorded = import("./voicePrerecorded.iui")()

  [firstInteractionInterceptor:, applyFirstInteractionHandlers:] = import("./firstInteraction.iui")()

  &areVoicesReady = false

  voice<?> = watch [isIOS, &audioDescriptionIsPrerecorded] {
    if !&areVoicesReady : * null ;
    if isIOS : * voiceSynthesized ; // Not usable on iOS since there is no audio ducking control
    if &audioDescriptionIsPrerecorded : * voicePrerecorded ; else : * voiceSynthesized ;
  }

  [ui: videoPlayerUi, onUiAttached: getVideoPlayer] = import("./videoPlayer.iui")(
    youtubeId: activeVideo.youtubeId,
    timeInterval: 400
  )

  rootUi = import("./rootUi.iui")(
    playlists:,
    videoPlayerUi:,
    audioDescriptionUi<?>: &audioDescriptionUi[&]<?>,
    activeVideo:,
    playContent:
    firstInteractionInterceptor:
  )

  document.body.appendChild(rootUi.element)

  videoPlayer = getVideoPlayer().await
  &videoPlayerPlay = videoPlayer.play

  audioDescription = import("./audioDescription.iui")(activeVideo:, videoPlayer:, voice<?>:).await
  &audioDescriptionIsPrerecorded = audioDescription.isPrerecorded
  &audioDescriptionUi = audioDescription.ui

  focusPreselectedVideo()

  applyFirstInteractionHandlers(onFirstInteraction: async (isKeyDown:, isVideoPlayerInteraction:) {
    concurrent(
      voiceSynthesized.getPermissions().unawait,
      voicePrerecorded.getPermissions().unawait,
    ).await
    &areVoicesReady = true
    if !isKeyDown && isVideoPlayerInteraction : videoPlayer.play() ;
  })
}

* (playlists:, otherVideos:, videoPlayer?:) {
  [document:, location:, window:] = import("core:web")
  
  &video = required()
  &playlist? = null
  &onUiComplete = () {}
  
  if location.hash {
    &onUiComplete = () {
      document.querySelector("#player-h2").focus(preventScroll: true)
      document
        .querySelector("tunes-player")
        .scrollIntoView(behavior: "instant", block: "start")
    }
  
    videoId = location.hash.slice(1)

    otherVideo? = otherVideos.find((video) { * video.id == videoId })
    if otherVideo? as otherVideo :
      &video = otherVideo
      *
    ;

    playlists.each((playlist) :
      playlist.videos.each((video) :
        if video.id == videoId :
          &video = video
          &playlist? = playlist
          *
        ;
      ;)
    ;)
  } else {
    // Cannot show a playlist with a content advisory by default because it would bypass the
    // permission dialog
    &playlist? = playlists.find((playlist) { * !playlist.needsContentAdvisory })
    &video = require playlist.videos[0]?
  }

  playContent = (newVideo?:, newPlaylist?:) {
    if newPlaylist?.needsContentAdvisory && !window.confirm(
      "This playlist contains content some viewers might find disturbing, are you sure you " +
        "want to continue?"
    ) : * ;

    &video = require newVideo?
    &playlist? = newPlaylist?

    document.querySelector("#player-h2").focus(preventScroll: true)
    document
      .querySelector("tunes-player")
      .scrollIntoView(behavior: "smooth", block: "start")
    videoPlayer?.play()
  }

  * [
    video: lock &video,
    playlist?: lock &playlist?,
    playContent:,
    onUiComplete: lock &onUiComplete
  ]
}
